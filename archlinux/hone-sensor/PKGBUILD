
_pkgname=hone-sensor
pkgname=$_pkgname-git
pkgver=20130729
pkgrel=1
pkgdesc="Hone device driver for correlating captured packets to processes"
url="https://github.com/HoneProject/Linux-Sensor"
license=(GPL2)
install=$_pkgname.install
arch=(i686 x86_64)
backup=(etc/udev/rules.d/60-hone.rules)
depends=(linux)
makedepends=(git linux-headers)
conflicts=(hone-sensor hone-sensor-dkms hone-sensor-dkms-git)
provides=(hone-sensor)
options=('!strip')
if [[ -n "$LOCAL_BUILD" ]]; then
  echo "Cloning from local filesystem"
  _giturl="$_pkgname::git+file://$(realpath $PWD/../..)"
else
  _giturl="$_pkgname::git+https://github.com/HoneProject/Linux-Sensor.git"
fi
source=("$_giturl" $install.in)
md5sums=(SKIP 0ed6996461d6a2779ad3d6587a5598ca)


prepare() {
  cd "$startdir"
  local _extramodules="$(readlink -f "/usr/lib/modules/${_kernver:-$(uname -r)}/extramodules")"
  sed -r 's@^(extramodules=).*$@\1'"$_extramodules"'@' $install.in > $install

  cd $_pkgname
  local _gitver=`git rev-parse --short HEAD`
  echo "$pkgver-$_gitver" > "$srcdir/$_pkgname/src/VERSION"
}


build() {
  cd "$srcdir/$_pkgname/src"

  local _modules="/usr/lib/modules/${_kernver:-$(uname -r)}"

  nm "$_modules/build/vmlinux" > System.map
  make KSRC="$_modules/build" SYSMAP="$PWD/System.map"
}


package() {
  cd "$srcdir/$_pkgname/src"

  local _modules="/usr/lib/modules/${_kernver:-$(uname -r)}"

  make KSRC="$_modules/build" SYSMAP="$PWD/System.map" INSTALL_MOD_PATH="$pkgdir/usr" modules_install

  mv "$pkgdir$_modules/"{extra,$(readlink $_modules/extramodules)}
  rmdir $pkgdir$_modules

  install -d "$pkgdir/etc/udev/rules.d"
  install -d "$pkgdir/usr/share/doc/hone"
  install -m 0644 udev.rules "$pkgdir/etc/udev/rules.d/60-hone.rules"
  install -m 0444 README "$pkgdir/usr/share/doc/hone/README"
}

# vim:set ts=2 sw=2 et:
