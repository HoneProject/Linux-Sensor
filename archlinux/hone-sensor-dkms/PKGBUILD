
_pkgname=hone-sensor
pkgname=$_pkgname-dkms-git
pkgver=20130729
pkgrel=1
pkgdesc="Hone device driver for correlating captured packets to processes"
url="https://github.com/HoneProject/Linux-Sensor"
license=(GPL2)
install=${pkgname%-git}.install
arch=(i686 x86_64)
backup=(etc/udev/rules.d/60-hone.rules)
depends=(dkms)
makedepends=(git)
conflicts=(hone-sensor hone-sensor-dkms hone-sensor-git)
provides=(hone-sensor)
options=('!strip')
if [[ -n "$LOCAL_BUILD" ]]; then
  echo "Cloning from local filesystem"
  _giturl="$_pkgname::git+file://`realpath $PWD/../..`"
else
  _giturl="$_pkgname::git+https://github.com/HoneProject/Linux-Sensor.git"
fi
source=("$_giturl" $install)
noextract=($_pkgname)
md5sums=(SKIP d75867119c411d206c468b1531625e6e)


package() {
  cd "$startdir/$_pkgname"
  
  _gitver=`git rev-parse --short HEAD`
  git archive HEAD src | tar -xC "$pkgdir" --xform 's@^src@usr/src/'"$_pkgname-$pkgver"'@'

  cd "$pkgdir/usr/src/$_pkgname-$pkgver"

  install -d "$pkgdir/etc/udev/rules.d"
  install -m 0644 udev.rules "$pkgdir/etc/udev/rules.d/60-hone.rules"
  rm -rf .gitignore debian hone.spec.in rhel6.patch udev.rules
  echo "$pkgver-$_gitver" > VERSION
  make dkms.conf DKMSNAME=$_pkgname DKMSVER=$pkgver
  sed -i -rf- dkms.conf <<"EOF"
/^AUTOINSTALL/a MAKE[0]="'make' System.map all KVER=$kernelver" \
CLEAN="make clean; rm -f System.map"
EOF
}

# vim:set ts=2 sw=2 et:
