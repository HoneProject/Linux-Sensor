
pkgbase=hone-git
pkgname=(hone-sensor-dkms-git hone-sensor-git hone-reader-git)
pkgver=20140116
pkgrel=1
pkgdesc="Correlate captured packets to processes"
url="https://github.com/HoneProject/Linux-Sensor"
license=(GPL2)
arch=(i686 x86_64)
makedepends=(git linux-headers)
if [[ -n "$LOCAL_BUILD" ]]; then
  echo "Cloning from local filesystem"
  _giturl="$pkgbase::git+file://`realpath $PWD/../..`"
else
  _giturl="$pkgbase::git+https://github.com/HoneProject/Linux-Sensor.git"
fi
source=("$_giturl"
        hone-sensor-dkms.install
        hone-sensor.install.in)
#noextract=($pkgbase)
md5sums=(SKIP
         d75867119c411d206c468b1531625e6e
         0ed6996461d6a2779ad3d6587a5598ca)


prepare() {
  cd "$startdir"

  local _extramodules="$(readlink -f "/usr/lib/modules/${_kernver:-$(uname -r)}/extramodules")"
  sed -r 's@^(extramodules=).*$@\1'"$_extramodules"'@' hone-sensor.install.in > hone-sensor.install

  cd "$pkgbase"
  local _gitver=`git rev-parse --short HEAD`
  echo "$pkgver-$_gitver" > "$srcdir/$pkgbase/src/VERSION"
}


build() {
  ## Build hone-sensor-git
  cd "$srcdir/$pkgbase/src"

  local _modules="/usr/lib/modules/${_kernver:-$(uname -r)}"

  nm "$_modules/build/vmlinux" > System.map
  make KSRC="$_modules/build" SYSMAP="$PWD/System.map"

  ## Build hone-reader-git
  cd "$srcdir/$pkgbase/logging"

  make
}


package_hone-sensor-dkms-git() {
  install=hone-sensor-dkms.install
  backup=(etc/udev/rules.d/60-hone.rules)
  depends=(dkms)
  conflicts=(hone-sensor hone-sensor-dkms hone-sensor-git)
  provides=(hone-sensor)
  options=('!strip')

  cd "$startdir/$pkgbase"
  
  _gitver=`git rev-parse --short HEAD`
  git archive HEAD src | tar -xC "$pkgdir" --xform 's@^src@usr/src/'"hone-sensor-$pkgver"'@'

  cd "$pkgdir/usr/src/hone-sensor-$pkgver"

  install -d "$pkgdir/etc/udev/rules.d"
  install -m 0644 udev.rules "$pkgdir/etc/udev/rules.d/60-hone.rules"
  rm -rf .gitignore debian hone.spec.in rhel6.patch udev.rules
  #echo "$pkgver-$_gitver" > VERSION
  make dkms.conf DKMSNAME=hone-sensor DKMSVER=$pkgver
  sed -i -rf- dkms.conf <<"EOF"
/^AUTOINSTALL/a MAKE[0]="'make' System.map all KVER=$kernelver" \
CLEAN="make clean; rm -f System.map"
EOF
}


package_hone-sensor-git() {
  install=hone-sensor.install
  backup=(etc/udev/rules.d/60-hone.rules)
  depends=(linux)
  conflicts=(hone-sensor hone-sensor-dkms hone-sensor-dkms-git)
  provides=(hone-sensor)
  options=('!strip')

  cd "$srcdir/$pkgbase/src"

  local _modules="/usr/lib/modules/${_kernver:-$(uname -r)}"

  make KSRC="$_modules/build" SYSMAP="$PWD/System.map" INSTALL_MOD_PATH="$pkgdir/usr" modules_install

  mv "$pkgdir$_modules/"{extra,$(readlink $_modules/extramodules)}
  rmdir $pkgdir$_modules

  install -d "$pkgdir/etc/udev/rules.d"
  install -d "$pkgdir/usr/share/doc/hone"
  install -m 0644 udev.rules "$pkgdir/etc/udev/rules.d/60-hone.rules"
  install -m 0444 README "$pkgdir/usr/share/doc/hone/README"
}


package_hone-reader-git() {
  depends=(hone-sensor)
  optdepends=('systemd: start hone captures on system startup'
              'logrotate: rotate capture files')
  conflicts=(hone-reader)
  provides=(hone-reader)

  cd "$srcdir/$pkgbase/logging"

  make install DESTDIR="$pkgdir" PREFIX="/usr/bin"
  install -Dm 0644 hone.logrotate "$pkgdir/etc/logrotate.d/hone"
  install -Dm 0644 hone.service "$pkgdir/usr/lib/systemd/system/hone.service"
  install -Dm 0644 hone.service "$pkgdir/usr/lib/systemd/system/hone-dkms.service"
  sed -i -r '/^#(After|Conflicts)=/{s/^#//;b};/^Conflicts=/s/^/#/' "$pkgdir/usr/lib/systemd/system/hone-dkms.service"
}

# vim:set sts=2 ts=2 sw=2 et:
